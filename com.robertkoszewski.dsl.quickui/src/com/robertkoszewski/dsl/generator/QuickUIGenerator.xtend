/*
 * generated by Xtext 2.10.0
 */
package com.robertkoszewski.dsl.generator

import com.robertkoszewski.dsl.quickUI.Package
import java.util.HashMap
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import com.robertkoszewski.dsl.quickUI.Window
import com.robertkoszewski.dsl.quickUI.Alias
import com.robertkoszewski.dsl.quickUI.JavaElement
import com.robertkoszewski.dsl.quickUI.Row
import com.robertkoszewski.dsl.quickUI.Label
import com.robertkoszewski.dsl.quickUI.Disabled
import com.robertkoszewski.dsl.quickUI.Checked
import com.robertkoszewski.dsl.quickUI.OnClick
import java.util.logging.Filter
import java.util.Set
import java.util.HashSet
import com.robertkoszewski.dsl.quickUI.Condition

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class QuickUIGenerator extends AbstractGenerator {
	
	HashMap<CharSequence, CharSequence> aliasMap
	Set<CharSequence> callbackMap
	HashMap<CharSequence, Condition> conditionMap

	// Generate Files
	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		// Build Alias Map
		aliasMap = new HashMap<CharSequence, CharSequence>()
		resource.allContents.filter(typeof(Alias)).forEach[buildAliasMap]

		// Generate Window
		resource.allContents.filter(typeof(Window)).forEach[buildWindowFile(fsa)]
		
	}
	
	// Build Alias Map
	def void buildAliasMap(Alias alias){
		aliasMap.put(alias.name, getPackageName(alias.package))
	}
	
	// Returns Full Qualified Package Name
	def CharSequence getPackageName(Package pkg) {
		return pkg.pname + if(pkg.subp === null) "" else "."+getPackageName(pkg.subp)
	}

	// Build Window File
	def buildWindowFile(Window window, IFileSystemAccess2 fsa) {		
		fsa.generateFile(window.name+".java", window.buildWindow)
	}
	
	// Initialize Variables (Per Window)
	def void initializeVariables(){
		conditionMap = new HashMap<CharSequence, Condition>()
		callbackMap = new HashSet<CharSequence>()
	}
	
	// Build Window Class
	def CharSequence buildWindow(Window window) '''
	«initializeVariables()»
	import javax.swing.*;
	import java.awt.*;
	import java.awt.event.*;

	public class «window.name» { 
		
		private JFrame window;
		
		«IF window.main == true»
		public static void main(String[] args){
			try { // Set System Look & Feel
		        UIManager.setLookAndFeel(UIManager.getSystemLookAndFeelClassName());
		    }catch (Exception e) {
		       // handle exception
		    }
		    
			«window.name» window = new «window.name»();
			try{
				window.show();
			}catch(Exception e){
				e.printStackTrace();
			}
		}
		
		«ENDIF»
		public «window.name»(){
			try{
				this.initialize();
			}catch(Exception e){
				e.printStackTrace();
			}
		}
		
		/**
		 * Initialize Window
		 */
		public void initialize(){
			window = new JFrame();
			window.setDefaultCloseOperation(JFrame.EXIT_ON_CLOSE);
			«IF window.title !== null»
			window.setTitle("«window.title»");
			«ENDIF»
			window.getContentPane().setLayout(new BoxLayout(window.getContentPane(), BoxLayout.Y_AXIS));
			
			«IF window.menu_elements !== null»
			«window.buildMenu»
			
			«ENDIF»
			«IF window.content_elements !== null»
			«window.buildContent»
			
			«ENDIF»
			«IF conditionMap.size !== 0»
			«buildConditions()»
			«ENDIF»
			window.pack();
			
		}
		
		
		/**
		 * Show Window
		 */
		public void show(){
			window.setVisible(true);
		}
		
		/**
		 * Hide Window
		 */
		public void hide(){
			window.setVisible(false);
		}
		
		/**
		 * Dispose Window
		 */
		public void dispose(){
			window.dispose();
		}
		
		«IF conditionMap.size !== 0»
		«buildMediator()»
		
		«ENDIF»
		«IF callbackMap.size !== 0»
		«buildCallbacks()»
		
		«ENDIF»
	}
	'''
	
	// Build Menu
	def CharSequence buildMenu(Window window) '''
	JMenuBar menuBar = new JMenuBar();
	window.setJMenuBar(menuBar);
	
	«FOR e: window.menu_elements»
		«e.buildElement("menuBar")»
	«ENDFOR»
	'''

	// Build Content
	def CharSequence buildContent(Window window) '''
	JPanel contentPanel = new JPanel();
	contentPanel.setLayout(new BoxLayout(contentPanel, BoxLayout.Y_AXIS));
	window.getContentPane().add(contentPanel);
	
	«FOR c: window.content_elements»
		«c.buildElement("contentPanel")»
	«ENDFOR»
	'''
	
	// Build Java Element
	def dispatch CharSequence buildElement(JavaElement element, CharSequence parentVarName) '''	
	«var varName = element.name.getVariableName»
	«element.alias.toClassPath» «varName» = new «element.alias.toClassPath»();
	«element.buildSpecialElement(varName)»
	
	«parentVarName».add(«varName»);

	«FOR sube: element.subelement»
	«sube.buildElement(varName)»
	«ENDFOR»
	'''

	// Build Row Element
	def dispatch CharSequence buildElement(Row row, CharSequence parentVarName) '''	
	«var varName = row.name.getVariableName»
	JPanel «varName»_row = new JPanel();
	«varName»_row.setLayout(new BoxLayout(«varName»_row, BoxLayout.Y_AXIS));
	«parentVarName».add(«varName»_row);
	
	JPanel «varName»_content = new JPanel();
	FlowLayout «varName»_fl = new FlowLayout();
	«varName»_fl.setAlignment(FlowLayout.LEFT);
	«varName»_content.setLayout(«varName»_fl);
	«varName»_row.add(«varName»_content);

	«FOR sube: row.subelement»
	«sube.buildElement(varName+"_content")»
	«ENDFOR»
	'''
	
	// Build Options
	
	// Build Option - Label
	def dispatch CharSequence buildElement(Label label, CharSequence parent_var) '''
	«parent_var».setText("«label.value»");
	'''
	
	// Build Option - Checked
	def dispatch CharSequence buildElement(Checked check, CharSequence parent_var) '''
	«parent_var».setSelected(«IF check.checked.isIsTrue == true»true«ELSE»false«ENDIF»);
	'''
	
	// Build Option - Disabled
	def dispatch CharSequence buildElement(Disabled dis, CharSequence parent_var) '''
	«conditionMap.put(parent_var, dis.condition)»
	'''
	
	// Build Option - Disabled -> Build Conditions
	def CharSequence buildConditions() '''
		// Conditions
		«FOR cond: conditionMap.entrySet»
		java.util.HashMap<Object, Condition> «cond.key»_conditions = new java.util.HashMap<Object, Condition>();
		«cond.value.processCondition(cond.key+"_conditions")»
		mediatorAddDisableOn(«cond.key», «cond.key»_conditions);
		
		«ENDFOR»
	'''
	
	// Build Option - Disabled -> Process Condition
	def CharSequence processCondition(Condition cond, CharSequence parent_condition_var)'''
	«parent_condition_var».put(«cond.element.name.getVariableName», «IF cond.negation»Condition.NONEMPTY«ELSE»Condition.EMPTY«ENDIF»);
	«IF cond.subcondition !== null»«cond.subcondition.processCondition(parent_condition_var)»«ENDIF»
	'''

	// Build Option - OnClick
	def dispatch CharSequence buildElement(OnClick onclick, CharSequence parent_var) '''
	«IF callbackMap.add(onclick.callback)»«ENDIF /* Dummy IF statement to avoid printing TRUE or FALSE to source */»
	«parent_var».addActionListener(new ActionListener() {
		public void actionPerformed(ActionEvent e) {
			action_«onclick.callback»(e);
		}
	});
	'''
	
	// Build Option - OnClick -> Build Callbacks
	def CharSequence buildCallbacks() '''
	/*  ################ *
	 *  Action Callbacks *
	 *  ################ */

	«FOR call: callbackMap»
	/**
	 * Action Callback for «call»
	 * @param ActionEvent
	 */
	private void action_«call»(ActionEvent e){
		// Handle Action Event from «call»
	}
	
	«ENDFOR»
	'''
	
	// Build Option - Filter
	def dispatch CharSequence buildElement(Filter filter, CharSequence parent_var) ''''''
	
	// Special Elements - Custom Fixes
	def CharSequence buildSpecialElement(JavaElement element, CharSequence variable){
		switch(toClassPath(element.alias)){	
			case "javax.swing.JTextField": // JTextField: Add Width
			return variable + ".setColumns(10);"
			
		}
	}
	
	// Mediator Function
	def CharSequence buildMediator() '''
	/**
	 * Mediator Function
	 * @param element
	 * @param conditions
	 */
	private void mediatorAddDisableOn(final Object element, java.util.Map<Object, Condition> conditions){
		JComponent component = (JComponent) element;
		
		Runnable callback = new Runnable(){
			@Override
			public void run() {
				boolean disabled = false;
				java.util.Iterator<java.util.Map.Entry<Object, Condition>> it = conditions.entrySet().iterator();
				while(it.hasNext()){ // Check all Conditions
					java.util.Map.Entry<Object, Condition> cond = it.next();
					Object target = cond.getKey();
					boolean empty = false;
					
					if(target instanceof javax.swing.text.JTextComponent) // Text Component
						empty = ((javax.swing.text.JTextComponent) target).getText().equals("");
					else if (target instanceof AbstractButton) // Abstract Button (CheckBoxes)
						empty = !((AbstractButton) target).isSelected();
					
					switch(cond.getValue()){
					case EMPTY:
						disabled = (empty?true:disabled);
						break;
					case NONEMPTY:
						disabled = (!empty?true:disabled);
						break;
					}
				}
				// Set Enabled
				component.setEnabled(!disabled); // Set Enabled State
			}
		};
		
		java.util.Iterator<Object> doi = conditions.keySet().iterator();
		while(doi.hasNext()){
			// Register Listeners
			Object target = doi.next();

			if(target instanceof javax.swing.text.JTextComponent){
				((javax.swing.text.JTextComponent)target).getDocument().addDocumentListener(new javax.swing.event.DocumentListener() {
				    @Override
				    public void insertUpdate(javax.swing.event.DocumentEvent e) {
				    	callback.run();
				    }

				    @Override
				    public void removeUpdate(javax.swing.event.DocumentEvent e) {
				    	callback.run();
				    }

				    @Override
				    public void changedUpdate(javax.swing.event.DocumentEvent e) {
				    	callback.run();
				    }
				});
			}else if(target instanceof AbstractButton){
				((AbstractButton)target).addActionListener(new ActionListener() {
					public void actionPerformed(ActionEvent e) {
						callback.run();
					}
				});
				
			}else{
				System.err.println("ERROR: Object of unsupported type");
			}
		}

		callback.run(); // Run Callback On Initialize
	}
	
	/**
	 * Conditions
	 */
	enum Condition{
		EMPTY,
		NONEMPTY
	}
	'''
	
	// Variable Generator
	int counter = 1;
	
	def CharSequence getVariableName(CharSequence variable){
		if(variable !== null)
			return "item_"+variable;
		return "uitem_" + counter++;
	}
	
	// Alias to Java Class
	def CharSequence toClassPath(Alias alias) {
		return aliasMap.get(alias.name);
	}
	
}
